v 4.9

/^"property": "squareRoot", "radicand": ([0-9]+)$/ ! Q 1
s//\1/

/^[01]$/ { p; d }

:n2h
s/[0-9]/@&/g
s/0//g
s/1/#/g
s/2/##/g
s/3/###/g
s/4/####/g
s/5/#####/g
s/6/######/g
s/7/#######/g
s/8/########/g
s/9/#########/g
:tens; s/#@/@##########/g; t tens
s/@//g

s/^/##%/; s/$/\n\f\n\v/; h; s/\n\f\n\v//

:mod; /^(#+)%\1/ { s//\1%/; x; s/\v/\v#/; x; /%#?$/ ! b mod }

/%#+$/ { g; s/^/#/; s/\v#+$/\v/; s/%\n/%/; h; s/\n.*//; /%#$/ ! b mod }

/%$/ {
    g
    s/%#+(\n.*\n\v)(#+)$/%\2\1/
    s/^(#+)(%.*\f)/\1\2\1*/
    h; s/\n.*//
    /%#$/ ! b mod
}
z; x; s/.*\f//; s/\n\v//

h; s/#+//g
/^(\*\*)+$/ ! c Unsupported
z; x

s/(#+)\*\1\*/\1*/g

s/^/@/
:multiply
s/@(#+)\*(#+)#\*/@\1@\1*\2*/
/\*#\*/{ s//*/; s/@//g; s/^/@/ }
/^[@#]+\*$/ ! b multiply
s/[^#]//g

:h2n
s/##########/@/g
s/@([0-9]*)([^0-9#@]|$)/@0\1\2/g
s/#########/9/g
s/########/8/g
s/#######/7/g
s/######/6/g
s/#####/5/g
s/####/4/g
s/###/3/g
s/##/2/g
s/#/1/g
s/@/#/g
t h2n
