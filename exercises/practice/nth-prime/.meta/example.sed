v 4.9

/^"property": "prime", "nth-prime": / ! Q 1
s///
/^-/ Q 1
/^0$/ {
    s/.*/there is no zeroth prime/ w /dev/stderr
    Q 1
}

:n2h
s/[0-9]/@&/g
s/0//g
s/1/#/g
s/2/##/g
s/3/###/g
s/4/####/g
s/5/#####/g
s/6/######/g
s/7/#######/g
s/8/########/g
s/9/#########/g
:tens; s/#@/@##########/g; t tens
s/@//g

s/$/@/
h
s/.*/##/

:low
/^###?$/ {
    x; s/$/#/
    /^(#+)@\1$/ { x; b h2n }
    x; s/^/#/
    b low
}

s/^/#/

:loop
s/^/%/; G; h; s/^%//
:div2; s/^##//; x; s/^/#/; x; /^##/ b div2; x
x; g; s/.*\n//; x; s/\n.*//
s/^(##)\1+%/#&/

:mod
/^(#+)%\1+$/ { s/#+%//; s/$/##/; b loop }

s/^##//
/^#{0,2}%/ ! b mod
s/.*%//; x; s/@/@#/

/^(#+)@\1$/ { x; b h2n }

x; s/^/##/

b loop

:h2n
s/##########/@/g
s/@([0-9]*)([^0-9#@]|$)/@0\1\2/g
s/#########/9/g
s/########/8/g
s/#######/7/g
s/######/6/g
s/#####/5/g
s/####/4/g
s/###/3/g
s/##/2/g
s/#/1/g
s/@/#/g
t h2n
