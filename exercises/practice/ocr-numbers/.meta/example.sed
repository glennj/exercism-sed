v 4.9

:loop
0~4 s/ +$/@/
$ ! N
$ ! b loop

/@/ ! {
    /^([^\n]{3})+(\n([^\n]{3})+)+$/ ! {
        s/.*/Number of input columns is not a multiple of three/
        q 1
    }

    /^[^\n]+\n[^\n]+\n[^\n]+\n[^\n]+(\n[^\n]+\n[^\n]+\n[^\n]+\n[^\n]+)*$/ ! {
        s/.*/Number of input lines is not a multiple of four/
        q 1
    }
}

s/\n +$//
s/\n@\n/@/g

:paste
s/^([^\n]+)\n([^@]+)@([^\n]+)\n(.*?)/\1\3\n\2@\4/
s/^([^\n]+\n[^\n]+)\n([^@]+)@([^\n]+)\n(.*)/\1\3\n\2@\4/
s/^([^@]+)@([^@]+)/\1,\2/
/@/ b paste

:ocr

s/^([^\n]{3})([^\n]*)\n([^\n]{3})([^\n]*)\n([^\n]{3})(.*)$/\1\3\5\n\2\n\4\n\6/

x;s/$/?/;x

/^ _ \| \|\|_\|\n/ { s///; x; s/\?$/0/; x }
/^     \|  \|\n/   { s///; x; s/\?$/1/; x }
/^ _  _\|\|_ \n/   { s///; x; s/\?$/2/; x }
/^ _  _\| _\|\n/   { s///; x; s/\?$/3/; x }
/^   \|_\|  \|\n/  { s///; x; s/\?$/4/; x }
/^ _ \|_  _\|\n/   { s///; x; s/\?$/5/; x }
/^ _ \|_ \|_\|\n/  { s///; x; s/\?$/6/; x }
/^ _   \|  \|\n/   { s///; x; s/\?$/7/; x }
/^ _ \|_\|\|_\|\n/ { s///; x; s/\?$/8/; x }
/^ _ \|_\| _\|\n/  { s///; x; s/\?$/9/; x }

x; /\?$/ { x; s/^[^\n]+\n// };

/[0-9]$/x

/^[^\n]+\n[^\n]+\n,/ { x; s/$/,/; x; s/,// }

/[^\n]/ b ocr

x
