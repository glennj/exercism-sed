v 4.9

s/count: //

/^0$/Q

s/[0-9]/@&/g
s/0//g
s/1/#/g
s/2/##/g
s/3/###/g
s/4/####/g
s/5/#####/g
s/6/######/g
s/7/#######/g
s/8/########/g
s/9/#########/g
:tens; s/#@/@##########/g; t tens
s/@//g

h; s/.*/#/; x;
/^#$/ { x; b hash2num }
s/#//
x; s/$/\n# #/
x; s/#//;
/^$/ { x; b hash2num }
x; H

:loop
s/.*\n//

:permut
/^(#+) (#+)(\s.*)?$/ {
    s//&\n\1\2/
    s/^#+ //
    t permut
}

s/^[^\n]*\n//
s/\n/ /g
s/^/# /
s/$/ #/
H
x
s/^#//
/^#+/ { x; b loop }
s/^\n//

:hash2num
s/##########/@/g
s/@([0-9]*) /@0\1 /g
s/#########/9/g
s/########/8/g
s/#######/7/g
s/######/6/g
s/#####/5/g
s/####/4/g
s/###/3/g
s/##/2/g
s/#/1/g
s/@/#/g
t hash2num
