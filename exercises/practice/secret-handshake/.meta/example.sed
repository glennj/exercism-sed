v 4.9

s/[0-9]/@&/g
s/0//g
s/1/#/g
s/2/##/g
s/3/###/g
s/4/####/g
s/5/#####/g
s/6/######/g
s/7/#######/g
s/8/########/g
s/9/#########/g
:tens; s/#@/@##########/g; t tens

s/^@@+/@/
h; s/#+$//; x
s/^@+/##%/

:mod
/^(##)%\1(.*)/ { s//\1%\2/; x; s/$/#/; x; b mod; }

/%$/  { x; s/^/0/; x }
/%#$/ { x; s/^/1/; x }

s/#$//

x
/@#/ {
    G
    s/@(#+)\n(##%)$/@\2\1/
    h
    s/#.*//
    x
    s/^[^@]+@//
    b mod
}

s/@$//; x; z; x
s/^/0000/; s/^.*(.{5})$/\1/

/....1$/ { x; s/$/\nwink/;            x }
/...1.$/ { x; s/$/\ndouble blink/;    x }
/..1..$/ { x; s/$/\nclose your eyes/; x }
/.1...$/ { x; s/$/\njump/;            x }
/1....$/ {
    x; s/^/@/
    :rev; s/^([^@]*)@([^@]*)\n([^\n]+)$/\1\n\3@\2/; t rev
    s/@//; x
}

x
s/^\n//
