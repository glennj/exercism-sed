v 4.9

/^"property": "score", / ! Q 1
s///

s/, ("category":)/\n\1/
h
s/\n.*//
s/[^0-9]+//g
/^[0-9]{5}$/ ! Q

H; z; x
s/^[^\n]+\n//

/^"category": / ! Q
s///

/^"ones"\n/   { s///; s/[^1]+//g; s/./#/g;      /^$/{s//0/p;d}; b h2n }
/^"twos"\n/   { s///; s/[^2]+//g; s/./##/g;     /^$/{s//0/p;d}; b h2n }
/^"threes"\n/ { s///; s/[^3]+//g; s/./###/g;    /^$/{s//0/p;d}; b h2n }
/^"fours"\n/  { s///; s/[^4]+//g; s/./####/g;   /^$/{s//0/p;d}; b h2n }
/^"fives"\n/  { s///; s/[^5]+//g; s/./#####/g;  /^$/{s//0/p;d}; b h2n }
/^"sixes"\n/  { s///; s/[^6]+//g; s/./######/g; /^$/{s//0/p;d}; b h2n }
/^"choice"\n/ { s///; b n2h }
/^"yacht"\n/  {
    /(.)\1\1\1\1/ { s/.*/##########/; s/.*/&&&&&/;  b h2n }
    c 0
}

/^"four of a kind"\n/ {
    s///
    s/(.).*\1.*\1.*\1/\1\1\1\1/
    /^.{4}$/ b n2h
    c 0
}

s/$/\n1:\n2:\n3:\n4:\n5:\n6:/
:count
s/\n([0-9])([^\n]*)(.*)\n\1:(.*)$/\n\2\3\n\1:#\4/
/\n\n/ ! b count

/^"full house"\n/ {
    s///
    s/[0-9]:##\n/\a&/
    s/[0-9]:###\n/\a&/
    /\a.*\a/ {
        s/\n[^\a\n]+//g
        s/^\n\n+/\n/
        s/\a//g
        s/:/*/g
        b n2h
    }
    c 0
}

/^"little straight"\n/ {
    s///
    /^\n1:#\n2:#\n3:#\n4:#\n5:#\n6:$/ {
        s/.*/##########/
        s/.*/&&&/
        b h2n
    }
    c 0
}

/^"big straight"\n/ {
    s///
    /^\n1:\n2:#\n3:#\n4:#\n5:#\n6:#$/ {
        s/.*/##########/
        s/.*/&&&/
        b h2n
    }
    c 0
}

:n2h
s/1/#/g
s/2/##/g
s/3/###/g
s/4/####/g
s/5/#####/g
s/6/######/g

/\*/ {
    s/\n/\n@/g
    :mult; s/@(#*)\*#(#+)/\1@\1*\2/; t mult
    s/\*#//g
    s/[@\n]//g
}

:h2n
s/##########/@/g
s/@([0-9]*)([^0-9#@]|$)/@0\1\2/g
s/#########/9/g
s/########/8/g
s/#######/7/g
s/######/6/g
s/#####/5/g
s/####/4/g
s/###/3/g
s/##/2/g
s/#/1/g
s/@/#/g
t h2n
