v 4.9

N;N;N
s/[[:space:]]+/ /g

/^\{ "property": "tally", "rows": \[/ ! Q 1
s///
s/^/\n/

:loop
s/\n[ "]+/\n/
s/[",]+\n/\n/
s/\n(.*);(.*);(.*)/\n\1:\3\n\2:/
s/\n([^:]+:win[^:]+):$/\n\1:loss/
s/\n([^:]+:loss[^:]+):$/\n\1:win/
s/\n([^:]+:draw[^:]+):$/\n\1:draw/
s/\n(.*):win/\n\1@MP:#\n\1@W:#/gm
s/\n(.*):loss/\n\1@MP:#\n\1@L:#/gm
s/\n(.*):draw/\n\1@MP:#\n\1@D:#/gm
$ ! { N; b loop }

s/\n\n+/\n/g
s/\s+\]\s+\}$//

:count
/\n([^\n]+)@([A-Z]+):(#+)(.*)\n\1@\2:#/ {
    s//\n\1@\2:\3#\4/
    t count
}
s/$/\n/

:concat
s/\n(([^@]+)@MP:[^\n]+)(.*)\n\2(@[A-Z]:#+)\n/\n\1\4\3\n/
t concat

s/@MP:#+/&@W:@D:@L:/g
:sc; s/@([A-Z]):([^\n]*)@\1:(#+)/@\1:\3\2/; t sc

s/@W:(#*)@D:(#*)@L:(#*)/@W:\1@D:\2@L:\3@P:\1*### \2/g
s/:\*###/:/g
s/\+\n/\n/g

:ml; s/:(#+)#\*(###)/:\1*\2 \2/g; t ml
s/# #/##/g
s/:#\*/:/g
s/: #/:#/g
s/ +\n/\n/g

:sr
s/\n([^@]+@[^\n]+@P:(#*))\n([^@]+@[^\n]+@P:\2#+)\n/\n\3\n\1\n/
t sr

s/\n(A[^@]+)/\n#\a\1/g
s/\n(B[^@]+)/\n##\a\1/g
s/\n(C[^@]+)/\n###\a\1/g
s/\n(D[^@]+)/\n####\a\1/g
s/\n(E[^@]+)/\n#####\a\1/g
s/\n(F[^@]+)/\n######\a\1/g
s/\n(G[^@]+)/\n#######\a\1/g
s/\n(H[^@]+)/\n########\a\1/g
s/\n(I[^@]+)/\n#########\a\1/g
s/\n(J[^@]+)/\n##########\a\1/g
s/\n(K[^@]+)/\n###########\a\1/g
s/\n(L[^@]+)/\n############\a\1/g
s/\n(M[^@]+)/\n#############\a\1/g
s/\n(N[^@]+)/\n##############\a\1/g
s/\n(O[^@]+)/\n###############\a\1/g
s/\n(P[^@]+)/\n################\a\1/g
s/\n(Q[^@]+)/\n#################\a\1/g
s/\n(R[^@]+)/\n##################\a\1/g
s/\n(S[^@]+)/\n###################\a\1/g
s/\n(T[^@]+)/\n####################\a\1/g
s/\n(U[^@]+)/\n#####################\a\1/g
s/\n(V[^@]+)/\n######################\a\1/g
s/\n(W[^@]+)/\n#######################\a\1/g
s/\n(X[^@]+)/\n########################\a\1/g
s/\n(Y[^@]+)/\n#########################\a\1/g
s/\n(Z[^@]+)/\n##########################\a\1/g

:sa
s/\n((#+)#+\a[^\n]+@P:(#+))\n(\2\a[^\n]+@P:\3)\n/\n\4\n\1\n/
t sa
s/\n#+\a/\n/g

s/@[A-Z]+:/,/g

:h2n
s/##########/@/g
s/@([0-9]*)([^0-9#@]|$)/@0\1\2/g
s/#########/9/g
s/########/8/g
s/#######/7/g
s/######/6/g
s/#####/5/g
s/####/4/g
s/###/3/g
s/##/2/g
s/#/1/g
s/@/#/g
t h2n

:zr; s/,,/,0,/; t zr
s/,\n/,0\n/g
s/([a-z]),/\1                              |/g
s/\n([^\n]{31}) */\n\1/g
s/\|([0-9]),/|  \1 ,/g
s/\|([0-9]{2}),/| \1 ,/g
s/\n/,\n/g
:pa;
s/,([0-9]),/|  \1 ,/; t pa
s/,([0-9]{2}),/| \1 ,/; t pa
s/[ ,]+\n/\n/g
s/,/ |  /g
s/^/Team                           | MP |  W |  D |  L |  P/
